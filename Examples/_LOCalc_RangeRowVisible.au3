#include <MsgBoxConstants.au3>

#include "..\LibreOfficeCalc.au3"

Example()

Func Example()
	Local $oDoc, $oSheet, $oRow
	Local $bVisible

	; Create a New, visible, Blank Libre Office Document.
	$oDoc = _LOCalc_DocCreate(True, False)
	If @error Then _ERROR($oDoc, "Failed to Create a new Calc Document. Error:" & @error & " Extended:" & @extended)

	; Retrieve the active Sheet.
	$oSheet = _LOCalc_SheetGetActive($oDoc)
	If @error Then _ERROR($oDoc, "Failed to retrieve the currently active Sheet Object. Error:" & @error & " Extended:" & @extended)

	; Retrieve Row 5's Object. Remember L.O. Rows are 0 based.
	$oRow = _LOCalc_RangeRowGetObjByPosition($oSheet, 4)
	If @error Then _ERROR($oDoc, "Failed to retrieve the Row Object. Error:" & @error & " Extended:" & @extended)

	; Retrieve Row 5's current visibility setting.
	$bVisible = _LOCalc_RangeRowVisible($oRow)
	If @error Then _ERROR($oDoc, "Failed to retrieve Row's current visibility setting. Error:" & @error & " Extended:" & @extended)

	MsgBox($MB_OK, "", "Is Row 5 currently visible? True/False: " & $bVisible)

	; Set Row 5 to invisible.
	_LOCalc_RangeRowVisible($oRow, False)
	If @error Then _ERROR($oDoc, "Failed to set Row's visibility setting. Error:" & @error & " Extended:" & @extended)

	; Retrieve Row 5's visibility setting again.
	$bVisible = _LOCalc_RangeRowVisible($oRow)
	If @error Then _ERROR($oDoc, "Failed to retrieve Row's current visibility setting. Error:" & @error & " Extended:" & @extended)

	MsgBox($MB_OK, "", "Now is Row 5 visible? True/False: " & $bVisible)

	MsgBox($MB_OK, "", "Press ok to close the document.")

	; Close the document.
	_LOCalc_DocClose($oDoc, False)
	If @error Then _ERROR($oDoc, "Failed to close opened L.O. Document. Error:" & @error & " Extended:" & @extended)

EndFunc

Func _ERROR($oDoc, $sErrorText)
	MsgBox($MB_OK, "Error", $sErrorText)
	If IsObj($oDoc) Then _LOCalc_DocClose($oDoc, False)
	Exit
EndFunc
