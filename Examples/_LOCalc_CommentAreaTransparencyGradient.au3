#include <MsgBoxConstants.au3>

#include "..\LibreOfficeCalc.au3"

Example()

Func Example()
	Local $oDoc, $oSheet, $oCell, $oComment
	Local $avSettings[0]

	; Create a New, visible, Blank Libre Office Document.
	$oDoc = _LOCalc_DocCreate(True, False)
	If @error Then _ERROR($oDoc, "Failed to Create a new Calc Document. Error:" & @error & " Extended:" & @extended)

	; Retrieve the active Sheet.
	$oSheet = _LOCalc_SheetGetActive($oDoc)
	If @error Then _ERROR($oDoc, "Failed to retrieve the currently active Sheet Object. Error:" & @error & " Extended:" & @extended)

	; Retrieve Cell B3
	$oCell = _LOCalc_RangeGetCellByName($oSheet, "B3")
	If @error Then _ERROR($oDoc, "Failed to retrieve Cell Object. Error:" & @error & " Extended:" & @extended)

	; Add a comment to Cell B3
	$oComment = _LOCalc_CommentAdd($oCell, "This is a Comment added by AutoIt!")
	If @error Then _ERROR($oDoc, "Failed to add a comment. Error:" & @error & " Extended:" & @extended)

	; Make the comment always visible
	_LOCalc_CommentVisible($oComment, True)
	If @error Then _ERROR($oDoc, "Failed to set comment visibility. Error:" & @error & " Extended:" & @extended)

	; Modify the Comment Transparency Gradient settings to: Gradient shape type = $LOC_GRAD_TYPE_SQUARE, Horizontal offset = 87%, Vertical offset = 55%, Angle = 56 degrees,
	; Set Transition start to 25%, Set beginning Transparency percentage to 75%, and the ending  percentage to 15%
	_LOCalc_CommentAreaTransparencyGradient($oDoc, $oComment, $LOC_GRAD_TYPE_SQUARE, 87, 55, 56, 25, 75, 15)
	If @error Then _ERROR($oDoc, "Failed to set Comment settings. Error:" & @error & " Extended:" & @extended)

	; Retrieve the current Comment settings. Return will be an array in order of function parameters.
	$avSettings = _LOCalc_CommentAreaTransparencyGradient($oDoc, $oComment)
	If @error Then _ERROR($oDoc, "Failed to retrieve Comment settings. Error:" & @error & " Extended:" & @extended)

	MsgBox($MB_OK, "", "The Comment's Transparency Gradient settings are as follows: " & @CRLF & _
			"The Transparency Gradient shape type is (see UDF constants): " & $avSettings[0] & @CRLF & _
			"The Horizontal offset percentage is: " & $avSettings[1] & @CRLF & _
			"The Vertical offset percentage is: " & $avSettings[2] & @CRLF & _
			"The Transparency Gradient rotation Angle is, in Degrees: " & $avSettings[3] & @CRLF & _
			"The transition start percentage is: " & $avSettings[4] & @CRLF & _
			"The beginning transparency percentage is: " & $avSettings[5] & @CRLF & _
			"The ending transparency percentage is: " & $avSettings[6])

	MsgBox($MB_OK, "", "Press ok to close the document.")

	; Close the document.
	_LOCalc_DocClose($oDoc, False)
	If @error Then _ERROR($oDoc, "Failed to close opened L.O. Document. Error:" & @error & " Extended:" & @extended)

EndFunc

Func _ERROR($oDoc, $sErrorText)
	MsgBox($MB_OK, "Error", $sErrorText)
	If IsObj($oDoc) Then _LOCalc_DocClose($oDoc, False)
	Exit
EndFunc
